//This file tests cases in the SPEC.md file. The things described there should work.

let assert_eq = fn(a, b) => {
    let _ = print("Comparing: " + a + " == " + b);
    if (a == b) {
        print("SUCCESS: Expected equals: " + a + " == " + b)
    } else {
        print("ERROR: Expected equals: " + a + " == " + b)
    }
};

let neg = fn(x) => {
    x - (2 * x)
};

//Rinha doesn't seem to support unary literals
let neg1 = 1 - 2;
let neg2 = neg(2);

let _ = assert_eq("-1", neg1 + "");
let _ = assert_eq("-2", neg2 + "");

let _ = assert_eq(1, 2); //this one should fail! it tests assert_eq works.

let _ = print("ˆˆˆˆˆˆ IGNORE THIS ERROR");

let _ = print("Testing Add binop:");
let _ = assert_eq(1, 1);
let _ = assert_eq(2, 1 + 1);
let _ = assert_eq(3 + 5, 8);
let _ = assert_eq("a" + 2, "a2");
let _ = assert_eq(2 + "a", "2a");
let _ = assert_eq("a" + "b", "ab");


//Sub BinOp
let _ = print("Testing Sub binop:");
let _ = assert_eq(0 - 1, neg1);
let _ = assert_eq(100 - 75, 25);
let _ = assert_eq(neg1 * 100 - 75, neg(175));
let _ = assert_eq(neg1 * 100 + 75, neg(25));


//Mul BinOp
let _ = print("Testing Mul binop:");
let _ = assert_eq(2 * 2, 4);
let _ = assert_eq(2 * neg2, neg(4));
let _ = assert_eq(neg2 * neg2, 4);
let _ = assert_eq(2 * neg2, neg(4));

//Div BinOp
let _ = print("Testing Div binop:");

let _ = assert_eq(40 / 2, 20);
let _ = assert_eq(neg(40) / 2, neg(20));
let _ = assert_eq(40 / neg2, neg(20));
let _ = assert_eq(neg(40) / neg2, 20);

//Rem BinOp
let _ = print("Testing Rem binop:");

let _ = assert_eq(40 % 2, 0);
let _ = assert_eq(3 % 2, 1);
let _ = assert_eq(neg(3) % 2, neg1);
let _ = assert_eq(3 % neg2, 1);
let _ = assert_eq(neg(3) % neg2, neg1);

//Eq BinOp
let _ = print("Testing Eq binop:");
let _ = assert_eq("a", "a");
let _ = assert_eq(true, true);
let _ = assert_eq(true == false, false);
let _ = assert_eq(false == false, true);
//the rest has been tested by the previous tests

//Neq BinOp
let _ = print("Testing new binop:");
let _ = assert_eq("a" != "b", true);
let _ = assert_eq("a" != "a", false);
let _ = assert_eq(true != false, true);
let _ = assert_eq(true != true, false);
let _ = assert_eq(1 != neg1, true);
let _ = assert_eq(1 != 1, false);

//Lt BinOp
let _ = print("Testing Lt binop:");
let _ = assert_eq(0 < 1, true);
let _ = assert_eq(1 < 0, false);
let _ = assert_eq(neg(10) < 1, true);
let _ = assert_eq(neg(10) < neg(5), true);
let _ = assert_eq(neg(10) < 10, true);
let _ = assert_eq(neg(10) < neg(20), false);

//Lte Binop
let _ = print("Testing Lte binop:");
let _ = assert_eq(0 <= 1, true);
let _ = assert_eq(0 <= 0, true);
let _ = assert_eq(1 <= 0, false);
let _ = assert_eq(1 <= 1, true);
let _ = assert_eq(neg(10) <= 1, true);
let _ = assert_eq(neg(10) <= neg(10), true);


//Gt Binop
let _ = print("Testing Gt binop:");
let _ = assert_eq(40 > 30, true);
let _ = assert_eq(40 > neg(30), true);
let _ = assert_eq(0 > 1, false);
let _ = assert_eq(0 > 0, false);
let _ = assert_eq(1 > 0, true);
let _ = assert_eq(1 > 1, false);
let _ = assert_eq(neg(10) > 1, false);
let _ = assert_eq(neg(10) > neg(10), false);

//Gte Binop
let _ = print("Testing Gte binop:");
let _ = assert_eq(40 >= 30, true);
let _ = assert_eq(40 >= neg(30), true);
let _ = assert_eq(0 >= 1, false);
let _ = assert_eq(0 >= 0, true);
let _ = assert_eq(1 >= 0, true);
let _ = assert_eq(1 >= 1, true);
let _ = assert_eq(neg(10) >= 1, false);
let _ = assert_eq(neg(10) >= neg(10), true);

//And BinOp
let _ = print("Testing And binop:");

let _ = assert_eq(true && true, true);
let _ = assert_eq(true && false, false);
let _ = assert_eq(false && false, false);
let _ = assert_eq(false && true, false);

//Or BinOp
let _ = print("Testing Or binop:");
let _ = assert_eq(true || true, true);
let _ = assert_eq(true || false, true);
let _ = assert_eq(false || false, false);
let _ = assert_eq(false || true, true);


let _ = print("Testing function that takes no params");
let f = fn() => {
    98090
};

let _ = assert_eq(f(), 98090);

let _ = print("Testing function that takes one param");

let f = fn(x) => {
    x
};

let _ = assert_eq(f(1), 1);

let _ = print("Testing function that takes two params");

let f = fn(x, y) => {
    x + y
};

let _ = assert_eq(f(44, 55), 99);

let _ = print("Testing function that returns a closure value");

let aaa = 987;

let f = fn() => {
    aaa
};

let _ = assert_eq(f(), 987);

let _ = print("Testing function that returns a closure value and takes a param");

let aaa = 987;

let f = fn(x) => {
    aaa + x
};

let _ = assert_eq(f(3), 990);

let _ = print("Testing nested closure behavior");

let empty_env = fn(v) => {
    ("ValErr", "undefined variable " + v)
};

let bind = fn(var, val, env) => {
    fn(v) => {
        if (var == v) {
            val
        } else {
            env(v)
        }
    }
};

let env = bind("z", 67, empty_env);
let env = bind("x", 32, env);
let env = bind("l", 18, env);
let env = bind("y", 12, env);

let v = env("x");
let _ = assert_eq(v, 32);

let _ = print("Testing return function closure");

let ret_func = fn() => {
    let x = 6754;
    fn() => {
        x
    }
};

let _ = assert_eq(ret_func()(), 6754);

let _ = print("Testing looping behavior [with TCO]");
let loop = fn (i, s) => {
   if (i == 0) {
     s
   } else {
      loop(i-1, s+1)
   }
};

let loop_result = loop(321, 0);

let _ = assert_eq(loop_result, 321);

let _ = print("Testing fib(15)");

let fib = fn (n) => {
  if (n < 2) {
    n
  } else {
    fib(n - 1) + fib(n - 2)
  }
};

let _ = assert_eq(fib(15), 610);

let _ = print("Testing closure behavior on recursive call");

let y = 100;
let x = fn(j) => { 
    if (j == 0) {
        1
    } else { 
        x(j-1) * y 
    } 
};

let _ = assert_eq(x(3), 1000000);


0